# Niri DMS Integration
# Exports flags so other modules can check what's DMS-managed
#
# When enabled, arroz.nix defers to DMS for these settings instead of
# providing its own defaults. Niri gracefully handles missing includes.
#
# Structure:
#   ~/.config/niri/arroz/  - arroz-managed files (Nix-generated)
#   ~/.config/niri/dms/    - DMS-managed files (runtime-generated)
#
{
  config,
  host,
  lib,
  pkgs,
  arrozInputs,
  ...
}:
let
  dms = host.desktop.niri.dms or { };
  playerctl = lib.getExe pkgs.playerctl;
  system = pkgs.stdenv.hostPlatform.system;
  vicinae = arrozInputs.vicinae.packages.${system}.default;

  # DMS keybinds KDL content
  # These are Nix-generated defaults that DMS can override at runtime
  # Placeholder file for DMS to use as initial template
  bindsPlaceholder = pkgs.writeText "niri-dms-binds.kdl" ''
    // DMS Keybindings for Niri
    // Generated by arroz.nix - DMS may override these at runtime

    binds {
        // System controls
        Ctrl+Alt+Delete { quit; }
        Ctrl+Alt+Escape { spawn "loginctl" "terminate-user" "$USER"; }

        // DMS controls
        Mod+A { spawn "dms" "ipc" "notifications" "toggle"; }
        Mod+Comma { spawn "dms" "ipc" "settings" "toggle"; }
        Mod+L { spawn "dms" "ipc" "lock"; }
        Mod+M { spawn "dms" "ipc" "processlist" "toggle"; }
        Mod+N { spawn "dms" "ipc" "night" "toggle"; }
        Mod+P { spawn "dms" "ipc" "notepad" "toggle"; }
        Mod+X { spawn "dms" "ipc" "powermenu" "toggle"; }

        // Application launchers
        Mod+E { spawn "${lib.getExe pkgs.vscode}"; }
        Mod+F { spawn "${lib.getExe pkgs.nautilus}"; }
        Mod+T { spawn "${lib.getExe pkgs.ghostty}"; }
        Mod+W { spawn "${lib.getExe pkgs.firefox}"; }

        // Vicinae (launcher/clipboard/switcher)
        Mod+Space { spawn "${vicinae}" "toggle"; }
        Mod+Period { spawn "${vicinae}" "vicinae://extensions/vicinae/core/search-emojis"; }
        Mod+V { spawn "${vicinae}" "vicinae://extensions/vicinae/clipboard/history"; }

        // Window/Column management
        Mod+Q { close-window; }
        Mod+C { center-column; }
        Mod+D { toggle-window-floating; }
        Mod+S { toggle-column-tabbed-display; }
        Mod+Alt+F { fullscreen-window; }
        Mod+Shift+A { toggle-overview; }
        Mod+F1 { show-hotkey-overlay; }

        // Window focus (Arrow keys)
        Mod+Left { focus-column-or-monitor-left; }
        Mod+Right { focus-column-or-monitor-right; }
        Mod+Up { focus-window-or-workspace-up; }
        Mod+Down { focus-window-or-workspace-down; }

        // Window movement
        Mod+Shift+Left { consume-or-expel-window-left; }
        Mod+Shift+Right { consume-or-expel-window-right; }
        Mod+Shift+Up { move-window-up; }
        Mod+Shift+Down { move-window-down; }

        // Monitor/Workspace movement
        Mod+Ctrl+Left { move-column-to-monitor-left; }
        Mod+Ctrl+Right { move-column-to-monitor-right; }
        Mod+Ctrl+Up { move-column-to-workspace-up; }
        Mod+Ctrl+Down { move-column-to-workspace-down; }

        // Window sizing
        Mod+Alt+Left { switch-preset-column-width-back; }
        Mod+Alt+Right { switch-preset-column-width; }
        Mod+Alt+Up { set-window-height "+10%"; }
        Mod+Alt+Down { set-window-height "-10%"; }

        // Screenshots
        Print { screenshot; }
        Shift+Print { screenshot-screen; }
        Super+Print { screenshot-window; }

        // Media controls (DMS audio)
        XF86AudioRaiseVolume { spawn "dms" "ipc" "audio" "increment" "3"; }
        XF86AudioLowerVolume { spawn "dms" "ipc" "audio" "decrement" "3"; }
        XF86AudioMute { spawn "dms" "ipc" "audio" "mute"; }
        XF86AudioMicMute { spawn "dms" "ipc" "audio" "micmute"; }

        // Media player controls
        XF86AudioPlay { spawn "${playerctl}" "play-pause"; }
        XF86AudioNext { spawn "${playerctl}" "next"; }
        XF86AudioPrev { spawn "${playerctl}" "previous"; }

        // Brightness controls (DMS)
        XF86MonBrightnessUp { spawn "dms" "ipc" "brightness" "increment" "5" ""; }
        XF86MonBrightnessDown { spawn "dms" "ipc" "brightness" "decrement" "5" ""; }
    }
  '';
in
{
  # Export flags so other modules can check what's DMS-managed
  options.arroz.niri.dms = {
    includeColors = lib.mkOption {
      type = lib.types.bool;
      default = dms.includeColors or false;
      internal = true;
      description = "Use DMS colors instead of arroz matugen colors";
    };
    includeLayout = lib.mkOption {
      type = lib.types.bool;
      default = dms.includeLayout or false;
      internal = true;
      description = "Use DMS layout instead of arroz layout settings";
    };
    includeBinds = lib.mkOption {
      type = lib.types.bool;
      default = dms.includeBinds or false;
      internal = true;
      description = "Use DMS binds instead of arroz keybindings";
    };
    includeRecents = lib.mkOption {
      type = lib.types.bool;
      default = dms.includeRecents or false;
      internal = true;
      description = "Use DMS recents instead of arroz recent-windows config";
    };
    includeOutputs = lib.mkOption {
      type = lib.types.bool;
      default = dms.includeOutputs or false;
      internal = true;
      description = "Include DMS outputs config for monitor management";
    };
    includeWpblur = lib.mkOption {
      type = lib.types.bool;
      default = dms.includeWpblur or false;
      internal = true;
      description = "Include DMS wallpaper blur config";
    };
  };

  # Create DMS keybinds file only if it doesn't exist (preserves user edits)
  config.home.activation = lib.mkIf config.arroz.niri.dms.includeBinds {
    createDmsBindsKdl = lib.hm.dag.entryAfter [ "writeBoundary" ] ''
      mkdir -p "$HOME/.config/niri/dms"
      [ -f "$HOME/.config/niri/dms/binds.kdl" ] || cp ${bindsPlaceholder} "$HOME/.config/niri/dms/binds.kdl"
    '';
  };
}
